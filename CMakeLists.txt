cmake_minimum_required(VERSION 3.17)
file(STRINGS configure.ac configure_ac REGEX ^AC_INIT)
string(REGEX MATCH "([0-9]+)[.][0-9]+[.][0-9]+" VER "${configure_ac}")
project(sodium VERSION ${VER} LANGUAGES C ASM)
include(GNUInstallDirs)
include(configure.cmake)
include(flags OPTIONAL)
set_property(GLOBAL PROPERTY USE_FOLDERS ON) # enables Solution Folders
option(DISABLE_TESTS "Disable tests" OFF)
option(ENABLE_BLOCKING_RANDOM "Enable only if /dev/urandom is totally broken on the target platform" OFF)
option(ENABLE_MINIMAL "Only compile the minimum set of functions required for the high-level API" OFF)
########################################
function(compareCmakeFilesys files)
  file(GLOB_RECURSE filesys RELATIVE ${CMAKE_CURRENT_LIST_DIR} *)
  file(GLOB_RECURSE ignorefiles RELATIVE ${CMAKE_CURRENT_LIST_DIR} .*.swp)
  if(ignorefiles)
    list(REMOVE_ITEM filesys ${ignorefiles})
  endif()
  foreach(f ${filesys})
    list(FIND files ${f} index)
    if(${index} GREATER -1)
      list(REMOVE_AT files ${index})
      list(REMOVE_ITEM filesys ${f})
    endif()
  endforeach()
  if(filesys)
    message(FATAL_ERROR "files not in cmake: ${filesys}")
  endif()
  foreach(f ${files})
    get_filename_component(absPath ${f} ABSOLUTE)
    if(NOT absPath MATCHES CMAKE_CURRENT_LIST_DIR)
      # remove any files that aren't under CMAKE_CURRENT_LIST_DIR
      list(REMOVE_ITEM files ${f})
    endif()
  endforeach()
  if(files)
    message(FATAL_ERROR "files not in repo, but in cmake: ${files}")
  endif()
endfunction()
########################################
add_subdirectory(src/libsodium)
if(NOT DISABLE_TESTS)
  enable_testing()
  add_subdirectory(test)
endif()
